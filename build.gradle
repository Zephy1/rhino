plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'jacoco'
    id 'distribution'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += 'toolsrc'
        }
    }
}

dependencies {
    testImplementation "junit:junit:4.12"
    testImplementation "org.yaml:snakeyaml:1.15"
    testImplementation "net.trajano.caliper:caliper:1.2.1"
}

test {
    exclude '**/*'
    useJUnit()

    exclude "**/benchmarks/**"

    jacoco.excludes = ['**/testsrc_tests_ecma_3_RegExp_perlstress*']

    systemProperty 'java.awt.headless', 'true'
    systemProperty 'mozilla.js.tests', 'testsrc/tests'
    systemProperty 'mozilla.js.tests.timeout', 15000
    systemProperty 'user.language', 'en'
    systemProperty 'user.country', 'US'
    systemProperty 'user.timezone', 'America/Los_Angeles'
    systemProperty 'file.encoding', 'UTF-8'

    maxHeapSize = "1g"
    testLogging.showStandardStreams = true
    forkEvery = 1
    maxParallelForks = 10
}

tasks.register("sunSpiderBenchmark", JavaExec) {
    mainClass = "com.google.caliper.runner.CaliperMain"
    systemProperty 'rhino.benchmark.report', "${buildDir.absolutePath}"
    args "-Cresults.upload.class=org.mozilla.javascript.benchmarks.ResultPlotter",
            "-i", "runtime",
            "org.mozilla.javascript.benchmarks.CaliperSpiderBenchmark.Spider"
    classpath = sourceSets.test.runtimeClasspath
}

tasks.register("v8Benchmark", Test) {
    jacoco.enabled = false
    include "**/benchmarks/V8Benchmark*"
    systemProperty 'rhino.benchmark.report', "${buildDir.absolutePath}"
    systemProperty 'file.encoding', 'UTF-8'
    workingDir = file("testsrc/benchmarks")
    maxHeapSize = "1g"
    testLogging.showStandardStreams = true
    forkEvery = 1
}

tasks.register("testBenchmark") {
    dependsOn "sunSpiderBenchmark", "v8Benchmark"
}

tasks.register("microBenchmark", JavaExec) {
    mainClass = "com.google.caliper.runner.CaliperMain"
    args "-i", "runtime",
            "org.mozilla.javascript.benchmarks.CaliperObjectBenchmark.FieldAccess",
            "-DstringKeys=100,1000",
            "-DintKeys=100,1000"
    classpath = sourceSets.test.runtimeClasspath
}

idea {
    module {
        excludeDirs += file('testsrc/tests/src')
        excludeDirs += file('buildGradle')
        excludeDirs += file('build')
        excludeDirs += file('.idea')
        excludeDirs += file('lib')
    }
}

jar {
    from "LICENSE.txt"
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "org.mozilla.javascript.tools.shell.Main",
                "Implementation-Version": project.version,
                "Implementation-Title":  "Mozilla Rhino",
                "Implementation-Vendor": "Mozilla Foundation",
                "Implementation-URL": "http://www.mozilla.org/rhino",
                "Built-Date": new Date().format("yyyy-MM-dd"),
                "Built-Time": new Date().format("HH:mm:ss"),
                "Bundle-ManifestVersion": "2",
                "Bundle-SymbolicName": "org.mozilla.rhino",
                "Bundle-Version": project.version.replaceAll("-.*", ""),
                "Export-Package": "org.mozilla.javascript,org.mozilla.javascript.ast,org.mozilla.javascript.annotations"
        )
    }
}

javadoc {
    options.addBooleanOption("-allow-script-in-comments", true)
    options.addStringOption('Xdoclint:html', '-quiet')
}

tasks.register("javadocJar", Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register("sourceJar", Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        create("rhino", MavenPublication) {
            groupId = 'com.chattriggers'
            artifactId = 'rhino'

            from components.java
            artifact(tasks.named("sourceJar"))
            artifact(tasks.named("javadocJar"))

            pom.withXml {
                def root = asNode()
                root.appendNode('description', """
Rhino is an open-source implementation of JavaScript written entirely in Java.
It is typically embedded into Java applications to provide scripting to end users.
""")
                root.appendNode("url", "https://developer.mozilla.org/en/Rhino")

                def l = root.appendNode("licenses").appendNode("license")
                l.appendNode("name", "Mozilla Public License, Version 2.0")
                l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

                def scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git@github.com:chattriggers/rhino.git")
                scm.appendNode("developerConnection", "scm:git:git@github.com:chattriggers/rhino.git")
                scm.appendNode("url", "git@github.com:chattriggers/rhino.git")

                def o = root.appendNode("organization")
                o.appendNode("name", "ChatTriggers")
                o.appendNode("url", "https://www.chattriggers.com/")
            }
        }
    }

    if (project.hasProperty("nexus_user") && !project.version.endsWith('-SNAPSHOT')) {
        repositories {
            maven {
                url = uri("https://repo.sk1er.club/repository/maven-releases/")
                credentials {
                    username = nexus_user
                    password = nexus_password
                }
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        html.outputLocation = file("${buildDir}/jacocoHtml")
    }
}

distributions {
    main {
        contents {
            from(sourceSets.main.java) {
                exclude 'man'
                into "rhino${project.version}/src"
            }
            from(sourceSets.main.resources) {
                exclude '**/*.java'
                into "rhino${project.version}/src"
            }
            from(javadoc.destinationDir) {
                into "rhino${project.version}/docs"
            }
            from(jar.outputs.files) {
                into "rhino${project.version}/lib"
            }
            from(sourceSets.main.allSource) {
                include 'man/*.1'
                into "rhino${project.version}"
            }
            from(file(".")) {
                include '*.txt', '*.md', 'build.gradle', 'build.properties', 'gradle.properties',
                        'gradle/**', 'gradlew'
                into "rhino${project.version}"
            }
            into "/"
        }
    }
}

distZip.dependsOn javadoc, jar
distTar.dependsOn javadoc, jar
